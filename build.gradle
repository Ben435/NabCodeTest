plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'war'
    id "com.moowork.node" version "1.3.1"
}

group = 'com.clare.ben'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    compile group: 'javax.validation', name: 'validation-api', version: '2.0.0.Final'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.2.Final'
    compile group: 'org.hibernate.validator', name: 'hibernate-validator-annotation-processor', version: '6.0.2.Final'

    compile group: 'com.google.guava', name: 'guava', version: '28.1-jre'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

    compileOnly 'org.projectlombok:lombok:1.18.10'
    annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

node {
    version = '10.16.3'
    npmVersion = '6.9.0'
    distBaseUrl = 'http://nodejs.org/dist'
    download = true

    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")

    nodeModulesDir = file("${project.projectDir}")
}

task buildFrontend(type: NpmTask) {
    args = ['run', 'build']
}

task testFrontend(type: NpmTask) {
    args = ['run', 'test']
}

buildFrontend.dependsOn(npmInstall)
testFrontend.dependsOn(npmInstall)
processResources.dependsOn(buildFrontend)
test.dependsOn(testFrontend)

